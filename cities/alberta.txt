//===== rAthena Script ======================================= 
//= Alberta Town
//===== By: ================================================== 
//= DZeroX
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena 1.0
//===== Description: ========================================= 
//= Town-specific Alberta NPCs
//===== Additional Comments: ================================= 
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//============================================================

alberta,97,51,0	script	Fabian	84,{
	mes "[Fabian]";
	mes "Hombre... Cuando viajas alrededor del mundo, escuchas algunas cosas loas.";
	next;
	mes "[Fabian]";
	mes "Una vez, escuché que hay Cards que contienen el poder de los monstruos. Si alguien logra tener en sus manos una carta, podrá ser capaz de usar el poder de ese monstruo.";
	next;
	mes "[Fabian]";
	mes "Supongo que es una especie de moda o estafa donde te hacen coleccionar todas las cartas o lo que sea. Quiero decir, ¡¿cómo puede una carta realmente tener el poder de un monstruo?!";
	next;
	mes "[Fabian]";
	mes "En serio...";
	close;
}

alberta,53,39,0	script	Steiner	100,{
	mes "[Steiner]";
	mes "¡Oh...!";
	mes "¡Bienivenido a Alberta";
	mes "joven aventurero!";
	next;
	mes "[Steiner]";
	mes "Perdóname si luzco distraído. Estoy pululando alrededor tratando de hacer un plan. Ya ves, escuché que hay una tienda en Geffen que vende una armadura que es resistente a la magia.";
	next;
	mes "[Steiner]";
	mes "Si compro muchas de ellas en cantidades y luego las revendo aquí por un precio mayor...";
	close;
}

alberta,20,183,0	script	Chad	49,{
	mes "[Chad]";
	mes "La gente dice que la legendaria arma Gungnir nunca falla sus golpes. Me pregunto si esto es posiblemente cierto...";
	next;
	mes "[Chad]";
	mes "También se dice que los bebés son emsamblados por las cigüeñas antes de la entrega, las chicas buscan chicos que se comportan como idiotas, ¡y que Santa Claus existe! Pero sólo en Lutie.";
	next;
	mes "[Chad]";
	mes "Me pregunto...";
	mes "Si algo de eso";
	mes "es posiblemente";
	mes "cierto...";
	close;
}

alberta,131,139,2	script	Drunken Old Man	54,{
	mes "[Deagle]";
	mes "^666666*Hiccup^666666*¡Hipp!*^000000*^000000";
	mes "¿Q-qué estás";
	mes "mirando? ¡¡Piérdete!!";
	next;
	switch(select("No decir nada.","Dejarlo solo.")) {
		case 1:
			mes "[Deagle]";
			mes "Hahahaha ^666666*Hip*^000000... Te pusiste nervioso. Puedo parecer inútil ahora, pero yo solía ser un marinero en el 'Going Mary'.";
			next;
			switch(select("Nuna he oído de él.","¿En serio? ¡No bromees!")) {
				case 1:
					mes "[Deagle]";
					mes "¡¿Nunca has oído de él?! Todo el mundo conoce al famoso barco pirata 'Going Mary' ^666666*Hip~*^000000";
					next;
					mes "[Deagle]";
					mes "Ah~ Los viejos tiempos. Si tan solo... Si tan solo no hubieras ido adentro de esa TORMENTA...^666666*Hip*^000000";
					next;
					mes "[Deagle]";
					mes "AH~ Capitán. Extraño nuestro capitán más que nada... Ningún enemigo sobrevivió a la espada del capitán.";
					close;
				case 2:
					mes "[Deagle]";
					mes "¡Así es! NADIE se mete con la tripulación del 'Going Mary' ¡Y nadie puede vencer a nuestro capitán en una pelea de espadas!";
					next;
					mes "[Deagle]";
					mes "¡¡¡CAPITÁN~!!! ^666666*Hip~*^000000 Él blandiría su espada así, luego... ¡¡LUEGO!!";
					next;
					mes "[Deagle]";
					mes "¡El bastardo del capitán estaba luchando, y cualquiera de sus amigos cerca de él estaban envueltos en llamas!";
					next;
					mes "[Deagle]";
					mes "Hombre, ¡esa espada debió haber tenido algún tipo de poder miterioso o el capitán era simplemente así de bueno...!";
					next;
					mes "[Deagle]";
					mes "Ufff~~ ^666666*Sollozo* *Sollozo...*^000000 Dios, ¡los extraño a todos! ¡Ahora estoy deprimido! Por favor, vete ya.";
					close;
			}
		case 2:
			mes "[Deagle]";
			mes "¡Así es!";
			mes "VETE~";
			close;
	}
}

alberta,58,80,2	script	Shakir	99,{
	mes "[Shakir]";
	if (rand(2)) {
		mes "Los Merchants tenemos nuestra propia habilidad de negociación cuando vendemos artículos. Esta habilidad puede darnos más dinero que cuando otra persona vende.";
		next;
		mes "[Shakir]";
		mes "Es algo más que sólo gritar '¡Tienes que darme más dinero por favor!' ¡Necesitas tener carisma y la retórica de un maestro!";
		next;
		mes "[Shakir]";
		mes "Podemos adquirir 24% más de zeny con está habilidad increíble. ¡¡Pero recuerda entrenar duro para adquirirla!!";
	} else {
		mes "Los Merchants podemos";
		mes "abrir tiendas ambulantes";
		mes "para hacer negocios.";
		next;
		mes "[Shakir]";
		mes "Con la habilidad Discount, podemos comprar cosas realmente baratas en las tiendas de las ciudades y cargarlas en el carro que rentamos.";
		next;
		mes "[Shakir]";
		mes "Así, ¡luego podemos viajar a cualquier lugar y vender nuestros ítems para obtener ganancias!";
		next;
		mes "[Shakir]";
		mes "De este modo, el negocio es más conveniente y seguro. No te distraigas, aunque es muy fácil hacerlo.";
	}
	close;
}

alberta,62,156,2	script	Sonya	102,{
	mes "[Sonya]";
	switch(rand(3)){
	case 0:
		mes "Hey, sabes, una vez estaba caminando por el bosque y vi un pequeño tallo verde moviéndose alrededor.";
		next;
		mes "[Sonya]";
		mes "Fui a ver de qué se trataba y cuando intenté tocarlo... ¡el tallo inmediatamente golpeó mi mano!";
		next;
		mes "[Sonya]";
		mes "Me sorprendí y salté hacia atrás un poco, luego me di cuenta que no era un tallo sino un animal muy pequeño.";
		next;
		mes "[Sonya]";
		mes "Tuve suerte de no molestarlo. Incluso el animal más pequeño puede ser peligroso si se enfurece.";
		close;
	case 1:
		mes "¿Conoces esos osos perezosos rastreadores que vivien en el bosque camino a Payon?";
		next;
		mes "[Sonya]";
		mes "Sólo por diversión, le tiré una roca a uno de ellos, ¡y de repente se abalanzó sobre mí! Estaba tannnnn asustado, que empecé a correr, y luego ¡¡BAM!!";
		next;
		mes "[Sonya]";
		mes "Se chocó contra una rama de un árbol pequeño y se noqueó a sí mismo. ¡Lo juro!, nunca provocaré un animal por diversión de nuevo.";
		close;
	case 2:
		mes "¡Una vez vi una manada de lobos atacar a uno de esos gigantes osos perezosos!";
		next;
		mes "[Sonya]";
		mes "Los lobos son mucho más unidos de lo que parecen. Si uno de ellos es atacado, cualquier lobo cercano correrá a ayudarlo.";
		next;
		mes "[Sonya]";
		mes "Lo pensaría dos veces si alguna vez quieres pelear cuando otros de su clase están cerca. Ten cuidado: ¡no dejes que se confabulen!";
		close;
	}
}

alberta,93,174,2	script	Grandmother Alma	103,{
	mes "[Grandmother Alma]";
	mes "Hace mucho tiempo,";
	mes "una nave abandonada";
	mes "encalló en";
	mes "el puerto de Alberta.";
	next;
	mes "[Grandmother Alma]";
	mes "Con la esperanza de salvar algunos sobrevivientes, varias personas de la ciudad de adentraron en la nave. Sin embargo, todos salieron estupefactos, diciendo que los cadáveres estaban caminando por todas partes adentro de la nave.";
	next;
	mes "[Grandmother Alma]";
	mes "El barco estaba también lleno de peligrosos organismos marinos y las personas no podían entrar, incluso si querían hacerlo.";
	next;
	mes "[Grandmother Alma]";
	mes "No pudimos hacer nada respecto a esa ominosa nave y sólo la dejamos como estaba. Hoy en día, equipos de exploración tratan de entrar al barco y acabar con sus monstruos.";
	next;
	mes "[Grandmother Alma]";
	mes "Podría ser una buena experiencia para una persona joven como tú ser un recluta. Pero, aún así, no vale la pena arriesgar tu vida si no eres lo suficientemente fuerte.";
	close;
}

alberta,189,151,5	script	Fisk	100,{
	mes "[Fisk]";
	mes "Qué tal compañero,";
	mes "¿A'ónde";
	mes "quieres ir?";
	next;
	switch(select("Sunken Ship -> 250 zeny.","Puerto de Izlude -> 500 zeny.","Olvídalo.")) {
		case 1:
			if (Zeny < 250) {
				mes "[Fisk]";
				mes "Hey amigo, ¡no trates de engañarme! ¡Dije 250 zeny!";
				close;
			}
			set Zeny,Zeny - 250;
			warp "alb2trea",43,53;
			end;
		case 2:
			if (Zeny < 500) {
				mes "[Fisk]";
				mes "¡No hay manera de llegar allá sin los 500 zeny primero!";
				close;
			}
			set Zeny,Zeny - 500;
			if(checkre(0))
				warp "izlude",195,212;
			else
				warp "izlude",176,182;
			end;
		case 3:
			mes "[Fisk]";
			mes "De acuerdo...";
			mes "Renacuajo.";
			close;
	}
}

alb2trea,39,50,6	script	Fisk#a2t	100,{
	mes "[Fisk]";
	mes "Entonces, ¿quieres regresar a la ciudad de Alberta, eh?";
	next;
	if(select("Sí por favor.","He cambiado de decisión.")==1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Paul	86,{
	mes "[Paul]";
	mes "Buen día~";
	mes "¿Te gustaría";
	mes "unirte al";
	mes "equipo de exploración";
	mes "del Sunken Ship?";
	next;
	mes "[Paul]";
	mes "¡Oh! Antes de que te unas, debo advertirte. Si no eres tan fuerte, quizá no quieras ir.";
	next;
	mes "[Paul]";
	mes "Y bien, ¿quieres";
	mes "registrarte?";
	mes "El costo de";
	mes "admisión es sólo";
	mes "200 Zeny.";
	next;
	switch(select("¡Apúntame!","Uh, no gracias.")) {
		case 1:
			if (Zeny < 200) {
				mes "[Paul]";
				mes "Parece que no tienes suficiente dinero, amigo. Pero por favor regresa cuando puedas pagar.";
				close;
			} else {
				set Zeny,Zeny - 200;
				warp "alb2trea",62,69;
				close;
			}
		case 2:
			mes "[Paul]";
			mes "Está bien...";
			mes "Estaré por aquí";
			mes "si cambias";
			mes "de opnión.";
			close;
	}
}

alberta,190,173,4	script	Phelix	85,{
	set .@weight,MaxWeight-Weight;
	mes "[Phelix]";
	if ((.@weight) < 10000) {
		mes "¡Espera un momento!";
		mes "¡Has comprado muchas cosas!";
		mes "¡No puedes recibir más ítems!";
		mes "Por favor disminuye la cantidad de ítems";
		mes "y ven a verme de nuevo.";
		close;
	}
	if (@event_zelopy == 0) {
		mes "¿Qué demonios estás haciendo aquí?";
		mes "No hay nada que puedas obtener gratis en este barco, si quieres algo, ¡trabaja por ello!";
		next;
		mes "[Phelix]";
		mes "Hmm, haber, por qué no me traes 10 jellopies y yo te doy 1 poción. ¿Qué te parece?";
		mes "O si eso es muy difícil para tu delicado culo, 3 jellopies por 1 Carrot.";
		next;
		mes "[Phelix]";
		mes "Si estás interesado en mi oferta, tráeme las cosas que mencioné.";
		set @event_zelopy,1;
		close;
	} else {
		mes "Hmm, quieres cambiar jellopies por Red Potions o algunos Carrots, ¿eh? Bien, ¿cuál quieres?";
		next;
		switch(select("Red Potions por favor.","Carrots por favor.")) {
		case 1:
			mes "[Phelix]";
			mes "De acuerdo...";
			mes "Veamos";
			mes "qué tienes...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 10) {
				mes "¡Hey! ¿No estabas escuchando? Dije 10 jellopies por 1 Red Potion, ¿eres sordo acaso?";
				close;
			} else {
				set .@max,countitem(909)/10;
				mes "Hmm, no está mal...";
				mes "¿Cuántas pociones";
				mes "quieres obtener?";
				next;
				switch(select("Tantas como pueda.","Quiero esta cantidad.","Ni lo pienses, me gustan mis jellopy.")) {
				case 1:
					delitem 909,.@max*10;	// Jellopy
					getitem 501,.@max;	// Red_Potion
					break;
				case 2:
					mes "[Phelix]";
					mes "No voy a darte más de 100  a la vez, así que no te molestes, ¿de acuerdo? Si no quieres ninguna, sólo escribe '0'.";
					mes "Ahora mismo, el total que puedes obtener es" + .@max + ", pero recuerda, 100 como máximo, ¿Acaso quieres romper mi espalda?";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount <= 0) {
						mes "Muchas gracias, vuelve en otra ocasión.";
						close;
					}
					if (.@amount > 100) {
						mes "¡Hey! ¿Qué te dije? 100 a la vez como máximo, ¡¿estás tratando de matarme o qué?!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Hmm, parece que no tienes suficientes. Ve a conseguir más jellopies si quieres algo más de mí.";
						close;
					}
					delitem 909,.@amount*10;	// Jellopy
					getitem 501,.@amount;	// Red_Potion
					break;
				case 3:
					mes "[Phelix]";
					mes "No hay problema,";
					mes "nos vemos luego.";
					close;
				}
				mes "[Phelix]";
				mes "¡Ahí está! Como lo prometí. No vayas a gastarlas todas al mismo tiempo.";
				close;
			}
		case 2:
			mes "[Phelix]";
			mes "Bien, veamos qué tienes...";
			next;
			mes "[Phelix]";
			if (countitem(909) < 3) {
				mes "Hmm, mira mariquita, dije 3 jellopies por 1 Carrot, ¿comprendes?";
				close;
			} else {
				set .@max,countitem(909)/3;
				mes "No está mal enclenque...";
				mes "¿Cuántas quieres?";
				next;
				switch(select("Tantas como pueda.","Quiero esta cantidad.","Olvídalo, me gustan mis jellopy.")) {
				case 1:
					delitem 909,.@max*3;	// Jellopy
					getitem 515,.@max;	// Carrot
					break;
				case 2:
					mes "[Phelix]";
					mes "Bien, no voy a darte más de 100 a la vez, así que no te molestes, ¿de acuerdo? Si no quieres ninguna, sólo escribe '0'.";
					input .@amount;
					next;
					mes "[Phelix]";
					if (.@amount == 0) {
						mes "Como quieras, nos vemos después.";
						close;
					}
					if (.@amount > 100) {
						mes "Hey idiota, dije 100 cómo máximo, ¡no más que eso! No voy a romper mi espalda porque te da la gana!";
						close;
					}
					if (countitem(909) < .@amount*10) {
						mes "Parece que no tienes suficientes. Ve a conseguir más si quieres algo.";
						close;
					}
					delitem 909,.@amount*3;	// Jellopy
					getitem 515,.@amount;	// Carrot
					break;
				case 3:
					mes "[Phelix]";
					mes "Nos pillamos luego.";
					close;
				}
				mes "[Phelix]";
				mes "¡Ahí lo tienes~! Como lo prometí. Trata de no embarrarte la cara.";
				close;
			}
		}
	}
}
